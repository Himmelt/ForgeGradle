//plugins {
//    id 'com.github.ben-manes.versions' version '0.20.0'
//}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = targetCompatibility = 1.8
compileJava {
    sourceCompatibility = targetCompatibility = 1.8
}

group = 'org.soraworld.gradle'
version = '3.0.196'

sourceSets {
    common
    mcp
    patcher
    userdev
}

configurations {
    configureEach {
        exclude group: 'org.ow2.asm'
    }
    mcpImplementation.extendsFrom commonImplementation
    patcherImplementation.extendsFrom commonImplementation
    userdevImplementation.extendsFrom mcpImplementation
    implementation.extendsFrom mcpImplementation, patcherImplementation, userdevImplementation
}

jar {
    from sourceSets.common.output
    from sourceSets.mcp.output
    from sourceSets.patcher.output
    from sourceSets.userdev.output
}

tasks.register('sourceJar', Jar) {
    sourceSets.configureEach {
        from it.allJava
    }
    classifier = 'sources'
}

artifacts {
    archives sourceJar
}

repositories {
    // maven { url 'http://localhost:8081/repository/maven-public/' }
    maven { url 'https://maven.aliyun.com/repository/public/' }
    maven { url 'https://maven.minecraftforge.net/' }
}

dependencies {
    commonImplementation gradleApi()
    commonImplementation 'com.cloudbees:diff4j:1.2'
    commonImplementation 'commons-io:commons-io:2.4'
    commonImplementation 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    commonImplementation 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    commonImplementation 'com.google.code.gson:gson:2.8.5'
    commonImplementation 'com.google.guava:guava:26.0-jre'
    commonImplementation 'de.siegmar:fastcsv:1.0.2'
    commonImplementation 'net.minecraftforge:artifactural:1.0.+'
    commonImplementation 'org.apache.maven:maven-artifact:3.6.0'
    commonImplementation 'org.apache.httpcomponents:httpclient:4.3.3'
    commonImplementation 'net.minecraftforge:srgutils:0.2.12'

    mcpImplementation sourceSets.common.output
    patcherImplementation sourceSets.mcp.output
    patcherImplementation sourceSets.common.output
    userdevImplementation sourceSets.mcp.output
    userdevImplementation sourceSets.common.output
}

publishing {
    publications {
        aliyun(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
    repositories {
        maven {
            url = 'https://packages.aliyun.com/5f4fa2da6207a1a8b17fa9f1/maven/minecraft'
            credentials {
                username = System.env.ALIYUN_MVN_USERNAME
                password = System.env.ALIYUN_MVN_PASSWORD
            }
        }
    }
}
